apply plugin: 'com.android.library'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.artifactory'

//region Release configuration

artifactoryPublish.dependsOn('build')

publishing {
    publications {
        cooeeJFrog(MavenPublication) {
            groupId = project.group.toString()  // com.letscooee
            artifactId = project.getName()      // "cooee-android-sdk"
            version = project.version.toString()

            // Customizing the POM file
            pom {
                name = 'Cooee Android SDK'
                description = 'Hyper-personalised Mobile App Re-Engagement via Machine Learning'
                url = 'https://github.com/letscooee/cooee-android-sdk'
                scm {
                    connection = 'scm:git:git://github.com/letscooee/cooee-android-sdk.git'
                    developerConnection = 'scm:git:ssh://github.com:letscooee/cooee-android-sdk.git'
                    url = 'https://github.com/letscooee/cooee-android-sdk/tree/master'
                }
            }

            project.afterEvaluate {
                artifact bundleReleaseAar

                pom.withXml {
                    // Make sure we copy dependencies to pom.xml
                    def dependencies = asNode().appendNode("dependencies")
                    configurations.getByName("releaseCompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                        def dependency = dependencies.appendNode("dependency")
                        dependency.appendNode("groupId", it.moduleGroup)
                        dependency.appendNode("artifactId", it.moduleName)
                        dependency.appendNode("version", it.moduleVersion)
                    }
                }
            }
        }
    }
}

artifactoryPublish {
    publications(publishing.publications.cooeeJFrog)
}

artifactory {
    clientConfig.setIncludeEnvVars(true)
    clientConfig.info.addEnvironmentProperty('test.adding.dynVar', new Date().toString())

    contextUrl = 'https://letscooee.jfrog.io/artifactory'
    publish {
        repository {
            repoKey = 'default-maven-local'
            username = "${artifactoryUser}"
            password = "${artifactoryPassword}"
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be published to Artifactory.
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            properties = ['qa.level': 'basic', 'dev.team': 'core']
            publishPom = true // Publish generated POM files to Artifactory (true by default)
        }
    }
}

//endregion

android {
    lintOptions {
        abortOnError false
    }

    compileSdkVersion 30
    buildToolsVersion "29.0.3"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        renderscriptTargetApi 29
        renderscriptSupportModeEnabled true
        versionCode project.version.toString().tokenize('.').collect { it.padLeft(2, '0') }.join('').toInteger()
        versionName project.version.toString()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        debug {
            buildConfigField "String", "SERVER_URL", '"https://api.sdk.letscooee.com"'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SERVER_URL", '"https://api.sdk.letscooee.com"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    api 'androidx.core:core:1.3.2'
    api 'androidx.appcompat:appcompat:1.2.0'
    api 'androidx.constraintlayout:constraintlayout:2.0.4'
    api 'com.google.android.gms:play-services-location:17.1.0'
    api 'com.github.bumptech.glide:glide:4.11.0'
    api platform('com.google.firebase:firebase-bom:26.2.0')
    api 'com.google.firebase:firebase-analytics'
    api 'com.google.firebase:firebase-messaging'
    api 'android.arch.lifecycle:extensions:2.2.0'
    api 'androidx.lifecycle:lifecycle-process:2.3.1'
    api 'com.github.bumptech.glide:compiler:4.11.0'
    api 'io.reactivex.rxjava3:rxandroid:3.0.0'
    api 'io.reactivex.rxjava3:rxjava:3.0.12'
    api "androidx.cardview:cardview:1.0.0"
    api 'jp.wasabeef:blurry:4.0.0'
    api 'com.google.android.flexbox:flexbox:3.0.0'
    api 'io.sentry:sentry-android:4.3.0'
    api 'com.squareup.okhttp3:okhttp:4.9.1'
    api 'com.google.code.gson:gson:2.8.7'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}