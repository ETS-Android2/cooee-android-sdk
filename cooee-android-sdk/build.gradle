apply plugin: 'maven-publish'
apply plugin: 'com.android.library'
apply plugin: 'com.google.gms.google-services'
String groupProjectID = project.group.toString()
String artifactProjectID = "cooee-android-sdk"
String publishVersionID = project.version.toString()
project.afterEvaluate {
    publishing {
        publications {
            aar(MavenPublication) {
                groupId groupProjectID
                artifactId artifactProjectID
                version publishVersionID
                artifact("$buildDir/outputs/aar/${project.getName()}-release.aar")
                pom.withXml {
                    def dependencies = asNode().appendNode("dependencies")
                    configurations.getByName("releaseCompileClasspath").getResolvedConfiguration().getFirstLevelModuleDependencies().each {
                        def dependency = dependencies.appendNode("dependency")
                        dependency.appendNode("groupId", it.moduleGroup)
                        dependency.appendNode("artifactId", it.moduleName)
                        dependency.appendNode("version", it.moduleVersion)
                    }
                }
            }
        }
    }
}
android {
    lintOptions {
        abortOnError false
    }

    compileSdkVersion 30
    buildToolsVersion "29.0.3"
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 30
        renderscriptTargetApi 29
        renderscriptSupportModeEnabled true
        versionCode project.version.toString().tokenize('.').collect { it.padLeft(2, '0') }.join('').toInteger()
        versionName project.version.toString()
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }
    buildTypes {
        debug {
            buildConfigField "String", "SERVER_URL", '"https://api.sdk.letscooee.com"'
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            buildConfigField "String", "SERVER_URL", '"https://api.sdk.letscooee.com"'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}
dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])
    api 'androidx.core:core:1.3.2'
    api 'androidx.appcompat:appcompat:1.2.0'
    api 'androidx.constraintlayout:constraintlayout:2.0.4'
    api 'com.squareup.retrofit2:retrofit:2.9.0'
    api 'com.squareup.retrofit2:converter-gson:2.9.0'
    api 'com.google.android.gms:play-services-location:17.1.0'
    api 'com.github.bumptech.glide:glide:4.11.0'
    api platform('com.google.firebase:firebase-bom:26.2.0')
    api 'com.google.firebase:firebase-analytics'
    api 'com.google.firebase:firebase-messaging'
    api 'android.arch.lifecycle:extensions:2.2.0'
    api 'androidx.lifecycle:lifecycle-process:2.2.0'
    api 'com.github.bumptech.glide:compiler:4.11.0'
    api 'io.reactivex.rxjava3:rxandroid:3.0.0'
    api 'io.reactivex.rxjava3:rxjava:3.0.0'
    api "androidx.cardview:cardview:1.0.0"
    api 'jp.wasabeef:blurry:4.0.0'
    api 'com.google.android:flexbox:2.0.1'
    api 'io.sentry:sentry-android:4.3.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.11.0'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
}